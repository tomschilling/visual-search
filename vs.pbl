## PEBL Visual search task
## Authors: Tom Schilling, Steven Maasch

define Start(par)
{
    gWin <- MakeWindow()
    gSleepEasy <- 1
    if(gSubNum + "" == "0")
    {
        gSubNum <- GetSubNum(gWin)
    }

    csvHeader <- "subNum,time,type,task,image,timestamp"
    gFileOut <- GetNewDataFile(gSubNum, gWin, "vs", "csv", csvHeader)

    ## Instructions
    MessageBox("Das ist die Anleitung... Drücken Sie die linke Maustaste, wenn Sie das gesuchte Objekt gefunden haben. Klicken Sie bitte auf OK, um mit dem Testdurchlauf zu beginnen.", gWin)
    CountDown(gWin)
    ShowCursor(0)

    ## Create task label and canvas
    gTaskLabel <- EasyLabel("", gVideoWidth / 2, gVideoHeight / 2, gWin, 32)
    gCanvas <- MakeCanvas(800, 800, MakeColor("white"))
    AddObject(gCanvas, gWin)
    Move(gCanvas, gVideoWidth / 2, gVideoHeight / 2)
    Hide(gCanvas)
    Draw()

    ## Trials
    trialsDefnFile <- "trialdefn.csv"
    trialsDefn <- ReadCSV(trialsDefnFile)
    testTrialDefnIndices <- [1]

    filterTestTrialDefn <- Repeat(0, Length(trialsDefn))
    loop(testTrialDefnIndex, testTrialDefnIndices)
    {
        SetElement(filterTestTrialDefn, testTrialDefnIndex, 1)
    }

    testTrialsDefn <- Filter(trialsDefn, filterTestTrialDefn)
    mainTrialsDefn <- RemoveSubset(trialsDefn, testTrialDefnIndices)
    mainTrialsDefnShuffled <- Shuffle(mainTrialsDefn)

    loop(testTrialDefn, testTrialsDefn)
    {
        TrialByTrialDefn(testTrialDefn)
    }

    MessageBox("Gut gemacht. Klicken Sie bitte auf OK, wenn Sie mit dem Hauptexperiment beginnen möchten.", gWin)
    CountDown(gWin)
    ShowCursor(0)

    loop(mainTrialDefn, mainTrialsDefnShuffled)
    {
        TrialByTrialDefn(mainTrialDefn)
    }

    ShowCursor(1)
    MessageBox("Wir bedanken uns für die Teilnahme an diesem Experiment.", gWin)
}

define TrialByTrialDefn(trialDefn)
{
    task <- Nth(trialDefn, 1)
    imageName <- Nth(trialDefn, 2)
    type <- Nth(trialDefn, 3)
    Trial(task, imageName, type)
}

define Trial(task, imageName, type)
{
    ResetCanvas(gCanvas)
    Hide(gCanvas)
    gTaskLabel.text <- task
    Draw()
    Wait(3000)

    image <- MakeImage(imageName)
    AddObject(image, gCanvas)
    Move(image, gCanvas.width / 2, gCanvas.height / 2)

    Show(gCanvas)
    Draw()
    t1 <- GetTime()
    WaitForDownClick()
    t2 <- GetTime()

    FilePrint(gFileOut, MakeCSVLine(t2 - t1, type, task, imageName))
}

define MakeCSVLine(time, type, task, image)
{
    return gSubNum + "," + time + "," + type + "," + task + "," + image + "," + TimeStamp()
}

define RemoveObjects(list,object)
{
    loop(i,list)
    {
        RemoveObject(i,object)
    }
}
